grammar de.nak.xtext.hausarbeit.rentalWorkflow.RentalWorkflow with org.eclipse.xtext.common.Terminals

generate rentalWorkflow "http://www.nak.de/xtext/hausarbeit/rentalWorkflow/RentalWorkflow"

RentalWorkflow:
	{RentalWorkflow}
	'defineWf' name=ID
	'events' (event+=Event)* 'end'
	'resetEvents' (resetEvents+=[Event])* 'end'
	'commands' (commands+=Command)* 'end'
	(states+=State)*;

Event:
	name=ID code=ID ofType=ofEventType;
	
Command:
	name=ID code=ID ofType=ofCommandType;
	
State:
	'state' name=ID
	ofType=OfType
	('actions' '{' (actions+=[Command])+ '}')?
	(transitions+=Transition)*
	'end';
	
Transition:
	event=[Event] '=>' state=[State];
	
enum OfType:
		initializing='initializing'
	|	creating='creating'
	|	beginning='beginning'
	|	running='running'
	|	canceling='canceling'
	|	replacing='replacing'
	|	storing='storing'
	|	finishing='finishing';
	
enum ofEventType:
		nextClicked='nextClicked'
	|	backClicked='backClicked';

enum ofCommandType:
		doSave='doSave'
	|	doAbord='doAbort'
	|	doDelete='doDelete';