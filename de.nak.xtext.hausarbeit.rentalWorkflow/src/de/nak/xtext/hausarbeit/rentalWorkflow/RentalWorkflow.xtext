grammar de.nak.xtext.hausarbeit.rentalWorkflow.RentalWorkflow with org.eclipse.xtext.common.Terminals

generate rentalWorkflow "http://www.nak.de/xtext/hausarbeit/rentalWorkflow/RentalWorkflow"

RentalWorkflow:
	{RentalWorkflow}
	'events' (event+=Event)* 'end'
	'resetEvents' (resetEvents+=[Event])* 'end'
	'commands' (commands+=Command)* 'end'
	(states+=State)*;

Event:
	name=ID code=ID;
	
Command:
	name=ID code=ID;
	
State:
	'state' name=ID
	ofType=OfType
	('actions' '{' (actions+=[Command])+ '}')?
	(transitions+=Transition)*
	'end';
	
Transition:
	event=[Event] '=>' state=[State];
	
enum OfType:
		initializing='initializing'
	|	creating='creating'
	|	beginning='beginning'
	|	running='running'
	|	cancelling='cancelling'
	|	replacing='replacing'
	|	enstoring='enstoring'
	|	finishing='finishing';